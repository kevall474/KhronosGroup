#_                   _ _ _  _ _____ _  _
#| | _______   ____ _| | | || |___  | || |
#| |/ / _ \ \ / / _` | | | || |_ / /| || |_
#|   <  __/\ V / (_| | | |__   _/ / |__   _|
#|_|\_\___| \_/ \__,_|_|_|  |_|/_/     |_|

#Maintainer: kevall474 <kevall474@tuta.io> <https://github.com/kevall474>
#Credits: Laurent Carlier <lordheavym@gmail.com>
#Credits: Lukas Jirkovsky <l.jirkovsky@gmail.com>

#######################################

#Set '1' to build with GCC
#Set '2' to build with CLANG
#Default is empty. It will build with GCC. To build with different compiler just use : env _compiler=(1 or 2) makepkg -s
if [ -z ${_compiler+x} ]; then
  _compiler=
fi

#######################################

pkgname=ocl-icd-git
pkgdesc='OpenCL ICD Bindings (git version)'
pkgver=2.3.0
pkgrel=1
arch=('x86_64')
url="https://github.com/OCL-dev/ocl-icd"
license=('Apache-2.0')
makedepends=('ruby' 'mesa' 'xmlto' 'xmltoman' 'python-xmltodict' 'asciidoc' 'opencl-headers' 'ninja' 'make' 'cmake' 'extra-cmake-modules' 'git'
             'clang' 'llvm' 'llvm-libs' 'gcc' 'gcc-libs')
depends=('glibc')
optdepends=('opencl-driver: packaged opencl driver')
conflicts=('libcl' 'ocl-icd')
replaces=('libcl')
provides=('opencl-icd-loader' 'ocl-icd' 'ocl-icd-git')
source=('ocl-icd::git+https://github.com/OCL-dev/ocl-icd.git')
md5sums=('SKIP')

pkgver(){
 cd ocl-icd
 echo 2.3.0.$(date -I | sed 's/-/_/' | sed 's/-/_/').$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
if [[ $_compiler = "1" ]]; then
  export CC="gcc"
  export CXX="g++"
elif [[ $_compiler = "2" ]]; then
  export CC="clang"
  export CXX="clang++"
else
  export CC="gcc"
  export CXX="g++"
fi

  cd ocl-icd

  autoreconf -fiv

  ./configure --prefix=/usr

  make -j$(nproc)
}

package_ocl-icd-git(){
  cd ocl-icd

  make DESTDIR="$pkgdir" install

  # remove headers. commit : 1cacbf8c358136655c18b5050f0f85e3d403d501
  # install headers
  #install -Dt "$pkgdir"/usr/include/CL/ -m644 OpenCL-ICD-Loader/include/*.h

  # install licence
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
